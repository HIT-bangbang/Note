# 已弃用

时间标志组功能在10.x版本以后可以完全被直接任务通知替代

在后续版本中，尽量不要使用事件标志组

# 事件标志组

一个事件标志组是内存中的一块32bit(3个Byte)的内存区域。每一位可以看作是一个Flag

每一个任务都可以对这32个bit中的某一位设置成1（Set）或者设置为0（clear）

其他任务可以等待这32个bit中的某一位或者某几位被置位（wait/sync）

## wait

例子，淘宝购物的例子。

任务跳转流程
* addtocartTask 创建三个任务 
* purchaseTask  将bit 0 设置为1 加入购物车，并等待所有购买流程结束（三个bit都设置为1）
* paymentTask 等待bit 0 设置为1 ，执行付款，并且将bit2也设置为1，删除任务
* inventoryTask 等待bit 0 1都设置为1，出库，并且将bit3也设置为1，删除任务
* purchaseTask 检测到bit 012 均设置为1，购买交易完成，删除任务

```c++

/*
   程序：  Event Group - Wait Bits
   公众号：孤独的二进制

   注意： 模拟器这里可能有个问题，按钮一次按钮后，
         有50%的几率一直保持LOW状态
         导致重复不断的购买
         没道理呀，大家可以一起来找Bug

   API:
   xEventGroupCreate
   xEventGroupSetBits
   xEventGroupWaitBits
   xEventGroupGetBits
   xEventGroupClearBits
*/

#define BTNPIN 23 //按钮

#define ADDTOCART_0	( 1 << 0 ) //0001 bit0
#define PAYMENT_1	( 1 << 1 )  //0010 bit1
#define INVENTORY_2	( 1 << 2 ) //0100 bit2

#define ALLBITS 0xFFFFFF //24bits都是1

EventGroupHandle_t xEventPurchase = NULL; //创建event handler
const TickType_t xTimeOut = 10000 / portTICK_PERIOD_MS; // 10秒
//const TickType_t xTimeOut = portMAX_DELAY; //无限等待

void addtocartTask(void *pvParam) {

  pinMode(BTNPIN, INPUT);

  while (1) {

    if (digitalRead(BTNPIN) == LOW) {
      Serial.println("用户真心决定下单了...");

      //放一些随机的延迟，否则运行的太快了，看不出效果
      for (int i = 0; i < random(100, 200); i++) vTaskDelay(10);

      xTaskCreate(purchaseTask, "Purchase", 1024 * 6, NULL, 1, NULL);
      xTaskCreate(paymentTask, "Payment", 1024 * 6, NULL, 1, NULL);
      xTaskCreate(inventoryTask, "Inventory", 1024 * 6, NULL, 1, NULL);
     
      vTaskDelay(120); //按钮防止抖动
    }

  }
}

void purchaseTask(void *pvParam) {

  EventBits_t uxBits;  // Event Group 24Bits 的 值

  while (1) {
    uxBits = xEventGroupSetBits(xEventPurchase, ADDTOCART_0); // 将bit 0 设置为1
    if ((uxBits & ADDTOCART_0)) {
      Serial.println("商品已经添加到了购物车，付款中...");
      Serial.print("   Event Group Value:");
      Serial.println(uxBits, BIN);
    }

    //等待三个bit全部设置为0
    uxBits = xEventGroupWaitBits (xEventPurchase,  //Event Group Handler
                                  ADDTOCART_0 | PAYMENT_1 | INVENTORY_2,     //等待Event Group中的那个Bit(s)
                                  pdFALSE,         //执行后，对应的Bits是否重置为 0
                                  pdTRUE,          //等待的Bits判断关系 True为 AND, False为 OR
                                  xTimeOut);

    if ((uxBits & ADDTOCART_0) && (uxBits & PAYMENT_1) && (uxBits & INVENTORY_2)) {
      //随机延迟, 模拟网页显示，恭喜买家入手商品
      for (int i = 0; i < random(100, 200); i++) vTaskDelay(10);

      xEventGroupClearBits(xEventPurchase, ALLBITS); //重置
      uxBits = xEventGroupGetBits(xEventPurchase); //读取

      Serial.println("交易完成, RESET Event Group");
      Serial.print("   Event Group Value:");
      Serial.println(uxBits, BIN);
      Serial.println("");
    }

    vTaskDelete(NULL);
    //vTaskDelay(10000);

  }
}

void paymentTask(void *pvParam) {
  while (1) {
    EventBits_t uxBits;

    uxBits = xEventGroupWaitBits (xEventPurchase,  //Event Group Handler
                                  ADDTOCART_0,     //等待Event Group中的那个Bit(s)
                                  pdFALSE,         //执行后，对应的Bits是否重置为 0
                                  pdTRUE,          //等待的Bits判断关系 True为 AND, False为 OR
                                  xTimeOut);

    // 代表ADDTOCART_0被设置为了 1
    if (uxBits & ADDTOCART_0) {

      //随机延迟, 模拟付款验证过程
      for (int i = 0; i < random(100, 200); i++) vTaskDelay(10);
      uxBits = xEventGroupSetBits(xEventPurchase, PAYMENT_1); // 将bit1 PAYMENT_1 设置为1

      Serial.println("支付宝付款完成,可以出货...");
      Serial.print("   Event Group Value:");
      Serial.println(uxBits, BIN);

      vTaskDelete(NULL);
    }

  }
}

void inventoryTask(void *pvParam) {
  EventBits_t uxBits;
  while (1) {

    uxBits = xEventGroupWaitBits (xEventPurchase,  //Event Group Handler
                                  ADDTOCART_0 | PAYMENT_1,     //等待Event Group中的那个Bit(s)
                                  pdFALSE,         //执行后，对应的Bits是否重置为 0
                                  pdTRUE,          //等待的Bits判断关系 True为 AND, False为 OR
                                  xTimeOut);

    // 判断 Event Group 中 ADDTOCART_0 和 PAYMENT_1 是否被设置为了0
    if ((uxBits & ADDTOCART_0) && (uxBits & PAYMENT_1))  {

      //随机延迟, 模拟仓库出货过程
      for (int i = 0; i < random(100, 200); i++) vTaskDelay(10);
      uxBits = xEventGroupSetBits(xEventPurchase, INVENTORY_2); // 将bit2 INVENTORY_2 设置为1

      Serial.println("仓库出货完成,快递已取货...");
      Serial.print("   Event Group Value:");
      Serial.println(uxBits, BIN);

      vTaskDelete(NULL);
    }

  }
}

void setup()
{
  Serial.begin(115200);
  xEventPurchase = xEventGroupCreate(); //创建 event group

  xTaskCreate(addtocartTask, "Add To Cart", 1024 * 4, NULL, 1, NULL);

}

void loop() {}

```


## sync

sync = set + wait

xEventGroupSync 就是自身先将某几位设置为1，然后再进入等待某几位都被设置成1

```c++

/*
   程序：  Event Group - SYNC + LCD
   公众号：孤独的二进制

   对于所有的共享资源，最好的方法就是放入到一个task中
   本案例将LCD放入到单独的task中，其他任务通过Quene来传递数据给LCD

       注意： 模拟器这里可能有个问题，按钮一次按钮后，
         有50%的几率一直保持LOW状态
         导致重复不断的购买
         没道理呀，大家可以一起来找Bug

   API:
   xEventGroupCreate
   xEventGroupSetBits
   xEventGroupGetBits
   xEventGroupClearBits

  xEventGroupSync( EventGroupHandle_t xEventGroup,
                   const EventBits_t uxBitsToSet,
                   const EventBits_t uxBitsToWaitFor,
                   TickType_t xTicksToWait );
*/
#include <LiquidCrystal_I2C.h>
LiquidCrystal_I2C lcd(0x27, 20, 4);

#define BTNPIN 23 //按钮

#define ADDTOCART_0	( 1 << 0 ) //0001 bit0
#define PAYMENT_1	( 1 << 1 )  //0010 bit1
#define INVENTORY_2	( 1 << 2 ) //0100 bit2

#define ALLBITS 0xFFFFFF //24bits都是1

#define BOUGHT_PAID_SENT (ADDTOCART_0 | PAYMENT_1 | INVENTORY_2)

EventGroupHandle_t xEventPurchase = NULL; //创建event handler
//const TickType_t xTimeOut = 10000 / portTICK_PERIOD_MS; // 10秒
const TickType_t xTimeOut = portMAX_DELAY; //无限等待

QueueHandle_t queueMsg = xQueueCreate(8, sizeof(char[20]));

void addtocartTask(void *pvParam) {

  pinMode(BTNPIN, INPUT);
  while (1) {

    if (digitalRead(BTNPIN) == LOW) {

      xEventGroupClearBits(xEventPurchase, ALLBITS); //重置

      String userID = "A: ";
      //Serial.println("用户真心决定下单了...");
      xQueueSend(queueMsg, &"Product's in cart", 0);
      //放一些随机的延迟，否则运行的太快了，看不出效果
      for (int i = 0; i < random(100, 200); i++) vTaskDelay(10);
      //Serial.println("商品已经添加到了购物车，付款中...");
      xQueueSend(queueMsg, &"Awaiting payment", 0);
      xTaskCreate(purchaseTask, "Purchase", 1024 * 6, NULL, 1, NULL);
      xTaskCreate(paymentTask, "Payment", 1024 * 6, NULL, 1, NULL);
      xTaskCreate(inventoryTask, "Inventory", 1024 * 6, NULL, 1, NULL);
      vTaskDelay(120); //按钮防止抖动

    }

  }
}

void lcdTask(void *pvLCD) {

  lcd.init();
  

  char line[20];
  byte lineCount = 0;

  while (1) {
    if (xQueueReceive(queueMsg, line, 2000) == pdPASS) {
      lcd.backlight();
      lcd.setCursor(0, lineCount);
      lcd.print("                    "); //clear this line
      lcd.setCursor(0, lineCount);
      lcd.print(line);

      lineCount++;

      if (lineCount == 4) { //翻页
        lineCount = 0;
        clearLCD();
      }

    } else {
      // Quene is empty for 5000 ticks, clear lcd
      clearLCD();
      lcd.noBacklight();
      lineCount = 0;
    }
  }
}

void clearLCD() {
  for (int i = 0; i < 4; i++) {
    lcd.setCursor(0, i);
    lcd.print("                    ");
  }
}

void purchaseTask(void *pvParam) {

  EventBits_t uxBits;  // Event Group 24Bits 的 值

  while (1) {


    uxBits = xEventGroupSync (xEventPurchase,  //Event Group Handler
                              ADDTOCART_0,     // 先将这个bit(s)设置为 1,然后再等待
                              BOUGHT_PAID_SENT,  //等待这些bits为 1
                              xTimeOut);

    if ((uxBits & BOUGHT_PAID_SENT) == BOUGHT_PAID_SENT)  {
      //Serial.println("purchaseTask,已经自我了断. ");
      xQueueSend(queueMsg, &"END purchaseTask", 0);
      vTaskDelete(NULL);
    }
  }
}

void paymentTask(void *pvParam) {

  EventBits_t uxBits;

  while (1) {
    //随机延迟, 模拟付款验证过程
    for (int i = 0; i < random(100, 200); i++) vTaskDelay(10);
    //Serial.println("支付宝付款完成,可以出货...");
    xQueueSend(queueMsg, &"Payment Received", 0);
    uxBits = xEventGroupSync (xEventPurchase,  //Event Group Handler
                              PAYMENT_1,     // 先将这个bit(s)设置为 1,然后再等待
                              BOUGHT_PAID_SENT,  //等待这些bits为 1
                              xTimeOut);

    if ((uxBits & BOUGHT_PAID_SENT) == BOUGHT_PAID_SENT)  {
      //Serial.println("paymentTask,已经自我了断. ");
      xQueueSend(queueMsg, &"END paymentTask", 0);
      vTaskDelete(NULL);
    }
  }
}

void inventoryTask(void *pvParam) {

  EventBits_t uxBits;

  while (1) {

    //随机延迟, 模拟仓库出货过程
    for (int i = 0; i < random(100, 200); i++) vTaskDelay(10);
    //Serial.println("仓库出货完成,快递已取货...");
    xQueueSend(queueMsg, &"Inventory Out", 0);

    uxBits = xEventGroupSync (xEventPurchase,  //Event Group Handler
                              INVENTORY_2,     // 先将这个bit(s)设置为 1,然后再等待
                              BOUGHT_PAID_SENT,  //等待这些bits为 1
                              xTimeOut);

    if ((uxBits & BOUGHT_PAID_SENT) == BOUGHT_PAID_SENT)  {
      //Serial.println("inventoryTask,已经自我了断. ");
      xQueueSend(queueMsg, &"END inventoryTask", 0);
      vTaskDelete(NULL);
    }
  }
}


void setup()
{
  Serial.begin(115200);
  xEventPurchase = xEventGroupCreate(); //创建 event group
  xTaskCreate(addtocartTask, "Add To Cart", 1024 * 4, NULL, 1, NULL);
  xTaskCreate(lcdTask, "LCD", 1024 * 6, NULL, 1, NULL);
}


void loop() {}

```