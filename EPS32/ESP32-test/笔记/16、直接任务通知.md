# 直接任务通知 Direct Task Notification

直接任务通知 可以替代事件标志组，信号量等绝大多数的功能，并且速度快45% 且占用更少的内存


一个直接任务通知由 一个 4 byte 的 **value** 和一个 1 byte 的 **state**

一个任务可以有多个直接任务通知


任务A可以直接修改任务B的某个直接任务通知中的value，任务B等待通知中的value中的某一位被修改后可以做出动作

所以，直接任务通知是任务直接之间进行的，以事件标志组和二进制信号量不同，不需要维护全局的变量。

state 记录了该任务通知的状态（pending 和 not pending），一共有三种状态:

```c++
    /* Values that can be assigned to the ucNotifyState member of the TCB. */
    #define taskNOT_WAITING_NOTIFICATION              ( ( uint8_t ) 0 ) /* Must be zero as it is the initialised value. */
    #define taskWAITING_NOTIFICATION                  ( ( uint8_t ) 1 )
    #define taskNOTIFICATION_RECEIVED                 ( ( uint8_t ) 2 )
```

state 不需要人为设置，由系统自动维护


一个任务可以有多个Direct Task Notification，最多可以有多少个通知存储在宏定义中：

```c++

configUSE_TASK_NOTIFICATIONS //系统是否开启了直接任务通知功能
configUSE_TASK_NOTIFICATION_ARRAY_ENTRIES //每个任务最多有几个通知

```

ESP32 Arduino中默认每个任务最多有1个通知，可以通过在系统中修改以上宏定义的值更改这个个数

```c++

/*
   程序：  Direct Task Notification
   要点：
      一个任务可以有多个notification
      每个notification包含4个字节的value 和 1个字节的stats
      stats用来记录当前的notification有没有被处理 pending or not pending
      我们不能对stats进行直接的读写操作，是系统自动的
      我们只能对notification value 进行操作

      本程序演示了对notification的读，写 和 等待。
      
*/

//任务句柄
static TaskHandle_t xTaskWait = NULL;
static TaskHandle_t xTaskGive = NULL;

#define LOWTHREEBITS ( 1UL << 0UL )|( 1UL << 1UL )|( 1UL << 2UL )

void taskWait(void *pvParam) {
  uint32_t ulNotificationValue; //用来存放本任务的4个字节的notification value
  BaseType_t xResult;

  while (1) {
    //vTaskDelay(1000);
    xResult = xTaskNotifyWait(0x00, //接到通知之前，先清除这几位，此处为不清除
                              0x00, //接到通知并且返回value值之后，清除这几位，此处为不清除。bits 0x00 or ULONG_MAX or 0xFFFFFFFF
                              &ulNotificationValue, //接到通知了，当前value的值notification value
                              portMAX_DELAY ); //超时事件，此处位为一直等待
                      
    if (xResult == pdTRUE) {
      Serial.println(ulNotificationValue); //将自己的notification value以二进制方式打出来
    } else {
      Serial.println("Timeout");
    }
  }
}

void taskGive(void *pvParam) {
  pinMode(23, INPUT_PULLUP);
  BaseType_t xResult;
  while (1) {
    if (digitalRead(23) == LOW) {
      xResult=xTaskNotify( xTaskWait, 0, eIncrement );  //通知，并将对方的notification value +1
      //xResult=xTaskNotify( xTaskWait, 0, eNoAction ); //仅通知，但不设置对方的notification value
      //xResult=xTaskNotify( xTaskWait, ( 1UL << 4UL ), eSetBits ); //仅将第3个bit设置为1 其他位不变（OR操作）
      //xResult=xTaskNotify( xTaskWait, LOWTHREEBITS, eSetValueWithOverwrite ); //覆盖原有的值
      //xResult=xTaskNotify( xTaskWait, 0b11111111, eSetValueWithOverwrite ); //覆盖原有的值
      //xResult=xTaskNotify( xTaskWait, 0xFF, eSetValueWithOverwrite ); //覆盖原有的值
      //xResult=xTaskNotify( xTaskWait, LOWTHREEBITS, eSetValueWithoutOverwrite); //如果没有待处理的就覆盖，如果有待处理的就pending
      Serial.println(xResult == pdPASS ? "成功\n":"失败\n");
      
      vTaskDelay(120); //消除按钮抖动
    }
  }
}
void setup() {
  Serial.begin(115200);

  xTaskCreate(taskGive, "", 1024 * 4, NULL, 1, &xTaskGive);
  xTaskCreate(taskWait, "", 1024 * 4, NULL, 1, &xTaskWait);
}

void loop() { }
```

## 直接任务通知取代二进制信号量

例子：通过按键，按一次灯亮，再按一次灯灭

```c++

/*
  程序：  使用Task Notification来取代Binary Semaphore

  API:  
  xTaskNotifyGive  // 相当于精简化的 xTaskNotify() + eIncrement
  ulTaskNotifyTake //  waitting for notification, then reset to 0

  公众号：孤独的二进制

*/

TaskHandle_t xflashLED = NULL;

void flashLED(void *pvParam) {
  uint32_t ulNotificationValue;
  pinMode(23, OUTPUT);
  while (1) {
    //返回Notification Value
    //命令含义: waitting for notification, then reset
    ulNotificationValue = ulTaskNotifyTake(pdTRUE, //pdTRUE 运行完后，清零Notification Value
                                           portMAX_DELAY);

    if ( ulNotificationValue > 0 )
    {
      digitalWrite(23, !digitalRead(23));
      vTaskDelay(1000);
    }
  }
}

void readBtn(void *pvParam) {

  pinMode(22, INPUT_PULLUP);

  while (1) {
    if (digitalRead(22) == LOW) {
      //命令含义，相当于精简化的 xTaskNotify() + eIncrement
      xTaskNotifyGive(xflashLED);
      vTaskDelay(120); //button debounce
    }
  }
}


void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200);

  xTaskCreate(flashLED, "Flash LED", 1024 * 4, NULL, 1, &xflashLED);
  xTaskCreate(readBtn, "Read Button", 1024 * 4, NULL, 1, NULL);

}

void loop() {
}

```

直接任务通知不能取代二进制信号量的情况：

多个任务都要随机获取一个二进制信号量的情况（信号接收方有多个）

例如要用一个按钮随机控制三个电机中的一个，就必须使用二进制信号量。因为直接任务通知是一个任务直接对另一个任务通知

二进制信号量有点类似于广播竞标，而直接任务通知类似于指派


## 直接任务通知取代事件组



```c++

static TaskHandle_t xledTask = NULL;

#define RESET_0  0b1
#define LEDBIT_1 0b10
#define LEDBIT_2 0b100
#define LEDBIT_3 0b1000
#define LEDBIT_4 0b10000
#define LEDBIT_5 0b100000
#define LEDBIT_6 0b1000000
#define LEDBIT_7 0b10000000
#define LEDBIT_8 0b100000000
#define LEDBIT_9 0b1000000000

// 模拟电话键盘的实现代码是抄的。
// 哪位大神有这个产品的链接，我来100个，实在是太复古了
void dialTask(void * pvParam) {
  const byte INDIALPIN = 33;
  const byte PULSEPIN = 32;
  pinMode(INDIALPIN, INPUT_PULLUP);
  pinMode(PULSEPIN, INPUT_PULLUP);

  byte counter = 0;
  boolean inDialPinLastState;
  boolean pulsPinLastState;

  inDialPinLastState = digitalRead(INDIALPIN);
  pulsPinLastState = digitalRead(PULSEPIN);

  while (1) {

    boolean inDialPinState = digitalRead(INDIALPIN);
    boolean pulsPinState = digitalRead(PULSEPIN);
    if (inDialPinState != inDialPinLastState) {
      if (!inDialPinState) {
        counter = 0;
      } else {
        if (counter) {
          counter = counter % 10;
          // ---- 横线上方Dial的判断代码为抄的 ----
          // Serial.println(counter);
          uint32_t ulEventGroup;
          switch (counter) {
            case 0: ulEventGroup = RESET_0 ; break;
            case 1: ulEventGroup = LEDBIT_1 ; break;
            case 2: ulEventGroup = LEDBIT_2 ; break;
            case 3: ulEventGroup = LEDBIT_3 ; break;
            case 4: ulEventGroup = LEDBIT_4 ; break;
            case 5: ulEventGroup = LEDBIT_5 ; break;
            case 6: ulEventGroup = LEDBIT_6 ; break;
            case 7: ulEventGroup = LEDBIT_7 ; break;
            case 8: ulEventGroup = LEDBIT_8 ; break;
            case 9: ulEventGroup = LEDBIT_9 ; break;
            default: ulEventGroup = RESET_0 ; break;
          }

          xTaskNotify( xledTask,
                       ulEventGroup,
                       eSetBits);

          // ---- 横线下方Dial的判断代码为抄的 ----
        }
      }
      inDialPinLastState = inDialPinState;
    }
    if (pulsPinLastState != pulsPinState) {
      if (!pulsPinLastState) {
        counter++;
      }
      pulsPinLastState = pulsPinState;
    }
  }

}

void ledTask(void *pvParam) {

  byte led_pins[9] = {23, 22, 21, 19, 18, 5, 15, 16, 4};
  for (byte pin : led_pins) pinMode(pin, OUTPUT);

  uint32_t ulNotifiedValue;

  while (1) {

    xTaskNotifyWait( pdFALSE,    /* Don't clear bits on entry. */
                     ULONG_MAX,        /* Clear all bits on exit. */
                     &ulNotifiedValue, /* Stores the notified value. */
                     portMAX_DELAY );

    // 如果第0位 为 0 熄灭所有灯
    if (ulNotifiedValue & (1 << 0) == 1) {
      for (int i = 1; i <= 9; i++) {
        digitalWrite(led_pins[i - 1], LOW);
      }
    }

    // 对0-9位进行判断，如果第一位是1，那么点亮第一个LED，以此类推
    for (int i = 1; i <= 9; i++) {
      if (ulNotifiedValue & (1 << i)) {
        digitalWrite(led_pins[i - 1], HIGH);
      }
    }

  }
}

void setup() {
  Serial.begin(115200);

  xTaskCreate(dialTask, "Dial Panel", 1024 * 10, NULL, 1, NULL);
  xTaskCreate(ledTask, "Nine LED", 1024 * 10, NULL, 1, &xledTask);

}

void loop() { }

```

## 直接任务通知当作邮箱

两个任务之间发送数据可以使用：

* Queue
* 全局变量 + Mutex
* 流媒体
* 消息缓存

也可以使用直接任务通知

直接任务通知有32bit的value，可以用于两个任务之间发送数据


用四个电位器 更改LED流水灯带的RGB值和速度

```c++

/*
   程序：  Direct Task Notification 邮箱功能
   要点：
      如此复杂的电灯程序，只是大家演示一下
      巧用任务的通知Value当作邮箱功能
      
      如何将4个uint8_t的数据打包成uint32_t,
      然后通过任务通知传递过去，
      并且在目的地进行拆解的方法。

      写完整个程序，感觉和走私，偷渡一样
      
   公众号：孤独的二进制
*/
#include <Adafruit_NeoPixel.h>
#define NEOPIN 33
#define NUMPIXELS 16

#define RPIN 34
#define GPIN 35
#define BPIN 32
#define SPIN 36 // speed

static TaskHandle_t xTaskNeoRing = NULL;

void configTask(void *pvParam) {
  pinMode(RPIN, INPUT);
  pinMode(GPIN, INPUT);
  pinMode(BPIN, INPUT);
  pinMode(SPIN, INPUT);
  uint8_t r_value = 0;
  uint8_t g_value = 0;
  uint8_t b_value = 0;
  uint8_t s_value = 100;

  while (1) {
    r_value = map(analogRead(RPIN), 0, 4095, 0, 255);
    g_value = map(analogRead(GPIN), 0, 4095, 0, 255);
    b_value = map(analogRead(BPIN), 0, 4095, 0, 255);
    s_value = map(analogRead(SPIN), 0, 4095, 10, 200);

    uint32_t rgb = s_value << 24 | r_value << 16 | g_value << 8 | b_value << 0;

    xTaskNotify(xTaskNeoRing, rgb, eSetValueWithOverwrite);

    vTaskDelay(100);
  }
}

void neoRing(void *pvParam) {

  Adafruit_NeoPixel pixels(NUMPIXELS, NEOPIN, NEO_GRB + NEO_KHZ800);
  pixels.begin();

  uint32_t srgb = 0;
  uint8_t r = 0;
  uint8_t g = 0;
  uint8_t b = 0;
  uint8_t s = 100;

  while (1) {

    pixels.clear();
    for (int i = 0; i < NUMPIXELS; i++) {

      xTaskNotifyWait(0x00, 0x00, &srgb, 0);
      s = (srgb & 0xff000000) >> 24;
      r = (srgb & 0x00ff0000) >> 16;
      g = (srgb & 0x0000ff00) >> 8;
      b = (srgb & 0x000000ff) >> 0;

      pixels.setPixelColor(i, pixels.Color(r, g, b));
      pixels.show();
      vTaskDelay(s * 5);
    }
  }
}

void setup() {
  Serial.begin(115200);

  xTaskCreate(configTask, "Configuration", 1024 * 10, NULL, 1, NULL);
  xTaskCreate(neoRing, "Neo Ring", 1024 * 20, NULL, 1, &xTaskNeoRing);

  vTaskDelete(NULL); //没我啥事了，自宫ba
}


void loop() {
}

```
