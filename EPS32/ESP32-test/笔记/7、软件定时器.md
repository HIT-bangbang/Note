# 软件定时器 software timer
```c++
#include <arduino.h>
/*
  API：
    xTimerCreate //创建时间
    xTimerStart //时间开始
    到时间后，会运行callback函数
*/

TimerHandle_t lockHandle, checkHandle;

void carKey(void *ptParam) {
  byte lockPin = 23;
  pinMode(lockPin, INPUT_PULLUP);

  for (;;) {
    if (digitalRead(lockPin) == LOW) {
      //timeout 3000 ticks
      //xTimerStart 只是开启时间而已，而不是创造时间对象
      //所以如果多次按按钮的话，不会有多个时间对象生成
      //多次按按钮相当于每次对timer进行reset xTimerReset()
      if (xTimerStart(lockHandle, 3000) == pdPASS) {
        Serial.println("About to lock the car");
      } else {
        Serial.println("Unable to lock the car");
      };
      vTaskDelay(100); //very rude Button Debounce
    }
  }
}

void lockCarCallback(TimerHandle_t xTimer) {
  Serial.println("Timer CallBack: Car is Locked");
}

void checkCallback(TimerHandle_t xTimer) {
  // ------- 很复杂的检测汽车Sensors的方法，时间不定 ---------
  Serial.print(xTaskGetTickCount());Serial.println("  -  All Sensors are working."); vTaskDelay(random(10, 90));
}

void setup() {
  Serial.begin(115200);
  xTaskCreate(carKey,
              "Check If Owner Press Lock Button",
              1024 * 1,
              NULL,
              1,
              NULL);

  lockHandle = xTimerCreate("Lock Car",
                            2000,
                            pdFALSE,
                            (void *)0,
                            lockCarCallback);

  checkHandle = xTimerCreate("Sensors Check",
                             100,
                             pdTRUE,
                             (void *)1,
                             checkCallback);
  //必须要在 portMAX_DELAY 内开启 timer start
  //portMAX_DELAY is listed as value for waiting indefinitely
  //实际上0xFFFFFFFF 2^32-1  49天 7周
  //在此期间，此task进入Block状态
  xTimerStart(checkHandle, portMAX_DELAY);
}
void loop() {
}

```
# 用法

## 1、创建句柄： 
    
    TimerHandle_t lockHandle, checkHandle;

## 2、创建定时器
定时器创建之后，并未开启定时

    lockHandle = xTimerCreate("Lock Car",           //定时器名称
                                2000,               //2000个tick触发
                                pdFALSE,            //模式，
                                (void *)0,          //定时器ID，要求必须是void*型
                                lockCarCallback);   //定时器回调函数
参数含义：
定时器名称
定时器周期，单位为tick
模式，pdFALSE为单次模式(只出发一次)，pdTRUE为周期模式，循环触发
定时器ID，当创建不同的定时器，但使用相同的回调函数时，在回调函数中通过不同的ID号来区分不同的定时器。
定时器回调函数。

返回值，创建成功返回定时器的句柄，由于FreeRTOSCongfig.h文件中heap空间不足，或者定时器周期设置为0，会返回NULL。

## 3、开启定时器


    xTimerStart(lockHandle,         //定时器句柄
                3000)               //成功启动定时器前的最大等待时间设置，单位系统时钟节拍

    返回值，返回pdFAIL表示此函数向消息队列发送消息失败，返回pdPASS表示此函数向消息队列发送消息成功。
    if (digitalRead(lockPin) == LOW) {………………}
